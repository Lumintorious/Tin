# Order in this file is important
external "
export const Anything = _S(Symbol(\"Anything\"), () => undefined, lazy(() => Type(\{_:\"Anything\"\}, \{_:(n) => true\}, \{_:(n) => true\})));
"

Type = struct:
	name: String 
	check = (obj: Anything) -> false: Boolean
	checkIntegrity = (obj: Anything) -> false: Boolean
	is = (parent: Type) -> false: Boolean

Type.of: (obj: Anything) -> Type = external

Primitive = Type & struct

Field = struct:
	name: String
	type: Type
	defaultValue: (() -> Anything)?

Parameter = struct:
	name: String?
	type: Type
	defaultValue: (() -> Anything)?

Intersection = Type & struct:
	left: Type
	right: Type

Union = Type & struct:
	left: Type
	right: Type

Singleton = Type & struct:
	value: Anything
	type: Type
	declaration: String?

Iterator = [T] => struct:
	next: () -> T?

AsyncIterator = [T] => struct:
	next: () ~> T?

Iterable = [T] => struct:
	makeIterator: () -> Iterator[T]

Seq = [T] => Iterable[T] & struct:
	at: (Number) -> T?
	length: () -> Number

Length = [N: Number] => Seq[Anything] where:
	if N :: Singleton,
		value = N.value
		if value :: Number,
			return self.length() == N.value
	false

# Guarantees O(1) access time
Array = [T] => Seq[T] & struct

VarSeq = [T] => struct:
	at: (Number) -> T?
	put: (Number, T) -> Ok?
	length: () -> Number

Struct = Type & struct:
	fields: Seq[Field]

Struct.accessDynamically: (self: Struct, obj: Anything, field: Field) -> Anything | Nothing = external
Struct.createDynamically: (self: Struct, fields: Seq[Anything]) -> Anything | Nothing = external

Lambda = Type & struct:
	params: Seq[Parameter]
	resultType: Type

RefType = Type & struct:
	get: () -> Type

Refinement = Type & struct:
	test: (any: Anything) -> Boolean

external "
//export const Nothing = _S(Symbol(\"Nothing\"), () => undefined, lazy(Type(\{_:\"Nothing\"}, \{_:(n) => n === null || n === undefined\}, \{_:(n) => n === null || n === undefined})))._and(Primitive());
export const Never = _S(Symbol(\"Never\"), () => undefined, lazy(Type(\{_:\"Never\"}, \{_:(n) => n === undefined}, \{_:(n) => n === undefined})))._and(Primitive());
export const Number = _S(Symbol(\"Number\"), (i) => Number(i), lazy(Type(\{_:\"Number\"}, \{_:(n) => typeof n === \"number\"\}, \{_:(n) => typeof n === \"number\"})))._and(Primitive());
export const String = _S(Symbol(\"String\"), (s) => String(s), lazy(Type(\{_:\"String\"}, \{_:(n) => typeof n === \"string\"}, \{_:(n) => typeof n === \"string\"})))._and(Primitive());
export const Boolean = _S(Symbol(\"Boolean\"), (s) => !!s, lazy(Type(\{_:\"Boolean\"}, \{_: (n) => typeof n === \"boolean\"}, \{_:(n) => typeof n === \"boolean\"})))._and(Primitive());
// export const ok = Symbol(\"ok\")
export const Ok = _S(Symbol(\"Ok\"), () => undefined, lazy(Type(\{_:\"Ok\"}, \{_: (n) => n === undefined}, \{_:(n) => n === undefined})))._and(Primitive());
"

jsonify: (obj: Anything) -> String = external
dejsonify: (json: String) -> Anything = external
assert: (condition: Boolean, message: String?) -> Nothing = external
print: (Anything) -> Ok = external
printRaw: (Anything) -> Ok = external
printTable: (Anything) -> Ok = external
debug: (Anything) -> Ok = external
debugThis: () -> Ok = external
clojure: (Anything) -> Ok = external
nothing: Nothing = external
ok: Ok = external
panic: (message: String) -> Never = external
Array.of: [T] -> (Seq[T]) -> Seq[T] = external
Array.empty: [T] -> Seq[T] = external
Seq.and: [T] -> (self: Seq[T], other: Seq[T]) -> Seq[T] = external
toString: (obj: Anything) -> String = external
listen: [T] -> (v: var T, onChange: (T) -> Ok) -> Ok = external
id = [T] -> (t: T) -> t
delay: (ms: Number, fn: () -> Ok) -> Ok = external
Console.useFieldNames = var true

Seq.mapSeq: [T, R] -> (self: Seq[T], fn: (T) -> R) -> Seq[R] = external
external "
	export function Seq$mapSeq(T, R) \{
		return function(fn) \{
			const self = this;
			const arr = _JsArr(self[Seq._s].length._())
			for (let i = 0; i < arr.length; i++) \{
				arr[i] = fn(self[Seq._s].at._(i))
			\}
			return Seq$createProperly(R)(
				arr
			)
		\}
	}
"


Like = [Str] => String where:
	if Str :: Singleton,
		v = Str.value
		if v :: String,
			return self.matches("^{v}$")
		
	false

Interpolation = struct:
	codeDisplay: String
	value: Anything

InterpolatedString = struct:
	elements: Seq[String | Interpolation]

Intersection.of = (left: Type, right: Type) ->
	check = (obj: Anything) ->
		left.check(obj) && right.check(obj)

	checkIntegrity = (obj: Anything) ->
		left.checkIntegrity(obj) && right.checkIntegrity(obj)

	Type { "{left.name} & {right.name}", check, checkIntegrity } & Intersection { left, right }

Digit = 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
One = 1

Byte = Number where:
	self >= 0 && self < 256

ToString = struct:
	toString: () -> String

Named = struct:
	name: String

Numbered = struct:
	number: Number

Integer = Number where:
	self % 1 == 0

Decimal = Number & !Integer

Positive = Number where:
	self >= 0

Negative = Number & !Positive

Uuid = ToString & struct:
	bytes: Array[Byte] & Length[16]

Identified = struct:
	id: Uuid

Uuid.genBytes: () -> Array[Byte] & Length[16] = external
Uuid.bytesToString: (Array[Byte]) -> String = external

Uuid.new = () ->
	@bytes = Uuid.genBytes()
	ToString { () -> Uuid.bytesToString(@bytes) } & Uuid { @bytes } 

external "
	export function Uuid$genBytes() \{
		const bytes = crypto.getRandomValues(new Uint8Array(16));
		bytes[6] = (bytes[6] & 0x0f) | 0x40; // version 4
		bytes[8] = (bytes[8] & 0x3f) | 0x80; // variant
		return Seq$createProperly(Byte)(bytes);
	}

	export function Uuid$bytesToString(bytes) \{
		const hex = [...bytes[Seq._s]._rawArray].map(b => b.toString(16).padStart(2, '0')).join('');
		return (
			hex.slice(0, 8) + '-' +
			hex.slice(8, 12) + '-' +
			hex.slice(12, 16) + '-' +
			hex.slice(16, 20) + '-' +
			hex.slice(20)
		);
}
"

Error = struct:
	message: String
	object: Anything?

Error.NOTFOUND = Error { message = "Not found"} & ToString { () -> "Not found" }
Error.EMPTY = Error { message = "Empty" } & ToString { () -> "Empty" }
Empty = Error.EMPTY

StackInfo = struct:
	stack: Seq[String]

Tuple2 = [A, B] => struct:
	left: A
	right: B

Tuple3 = [A, B, C] => struct:
	left: A
	middle: B
	right: C

String.matches: (self: String, regex: String) -> Boolean = external
String.length: (self: String) -> Number = external
String.isBlank = (self: String) -> self.length() == 0

external "
export function String$length() \{
	return this.length
\}
"

Type.allFields = (self: Type): Seq[(Struct, Field)] ->
	if self :: Struct,
		mapper = (field: Field) -> (self, field: Field)
		self.fields.mapSeq(mapper)
	else if self :: Intersection,
		(self.left.allFields()) .and (self.right.allFields())
	else
		Array.empty[(Struct, Field)]

Type.allPossibilities = (self: Type): Seq[Anything] ->
	if self :: Singleton,
		Array.of(self.value)
	else if self :: Union,
		(self.left.allPossibilities()) .and (self.right.allPossibilities())
	else
		Array.empty[Anything]

Varray.of: [T] -> (length: Number, filler: T) -> VarSeq[T] = external
	
external "
export const Varray$of = (T) => (len, obj) => \{
	const jarr = _JsArr(len).fill(obj)
	return VarSeq(T)(
		\{_: (i) => jarr[i]},
		\{_: (i, obj) => jarr[i] = obj},
		\{_: () => jarr.length}
	)
\}
"

